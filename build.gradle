// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
	
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.2'
        

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    // NOTE:
    // there are three places to change the version number
    // this file
    // app: build.gradle (versionCode only)
    // OsmTrack (version and versionDate)
    project.version  "1.6.3"
    group 'org.btools'
	
    repositories {
        mavenCentral()
        google()
    }
	
    apply plugin: "maven-publish"
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/$System.env.REPO")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication)
        }
    }

    apply plugin: "checkstyle"
}

// jacoco plugin depends on loaded java/java-library plugin specified in children's build.gradle
evaluationDependsOnChildren()

subprojects {
    apply plugin: "jacoco"

    jacocoTestReport {
        reports {
            csv.enabled true
        }
    }

    test {
        finalizedBy jacocoTestReport // report is always generated after tests run
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
